plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id 'org.jetbrains.dokka' version '0.9.18'
}

android {
    compileSdkVersion compileSdk
    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':support-ext')
    implementation project(':support-data')

    /** Koin AndroidX Dependency Injection */
    implementation "org.koin:koin-core:$koin"
    implementation "org.koin:koin-core-ext:$koin"

    /** Material Design */
    implementation "com.google.android.material:material:$material"

    /** Kotlin Libraries */
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesCore"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesAndroid"

    /** Architecture Components */
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleStateX"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleX"
    implementation "androidx.work:work-runtime-ktx:$workerKtx"

    /** Timber */
    implementation "com.jakewharton.timber:timber:$timber"

    /** Testing-only dependencies */
    testImplementation "junit:junit:$junit"
    testImplementation "io.mockk:mockk:$mockk"
    androidTestImplementation "androidx.test:runner:$runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
}

dokka {
    moduleName = 'core'
    outputFormat = 'kotlin-website'
    outputDirectory = "$buildDir/javadoc"
    reportUndocumented = true

    linkMapping {
        dir = "src/main/java"
        url = "https://github.com/anitrend/support-arch/tree/develop/paging/support-core/src/main/java"
        suffix = "#L"
    }
}

task dokkaJar(type: Jar) {
    setArchiveClassifier('javadoc')
    from dokka
}

task sourcesJar(type: Jar) {
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    setArchiveClassifier('javadoc')
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}