plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id 'org.jetbrains.dokka' version '1.4.10.2'
}

android {
    compileSdkVersion compileSdk
    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':support-ext')
    implementation project(':support-data')
    implementation project(':support-domain')

    /** Koin AndroidX Dependency Injection */
    implementation("org.koin:koin-core:$koin")
    implementation("org.koin:koin-core-ext:$koin")

    /** Material Design */
    implementation("com.google.android.material:material:$material")

    /** Kotlin Libraries */
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesCore")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesAndroid")

    /** Architecture Components */
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleX")
    implementation("androidx.lifecycle:lifecycle-extensions:$lifecycleX")
    implementation("androidx.work:work-runtime-ktx:$workerKtx")

    /** Timber */
    implementation("com.jakewharton.timber:timber:$timber")

    /** Testing-only dependencies */
    testImplementation("junit:junit:$junit")
    testImplementation("io.mockk:mockk:$mockk")
    androidTestImplementation("androidx.test:runner:$runner")
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/docs/javadoc"

    configuration {
        moduleName = 'core'
        reportUndocumented = true
        platform = "JVM"
        jdkVersion = 8

        perPackageOption {
            prefix = "kotlin"
            skipDeprecated = false
            reportUndocumented = true
            includeNonPublic = false
        }

        sourceLink {
            path = "src/main/kotlin"
            url = "https://github.com/anitrend/support-arch/tree/develop/support-core/src/main/kotlin"
            lineSuffix = "#L"
        }

        externalDocumentationLink {
            url = new URL("https://javadoc.jitpack.io/com/github/anitrend/support-arch/support-ext/1.3.0-beta02/javadoc/")
            url = new URL("https://javadoc.jitpack.io/com/github/anitrend/support-arch/support-data/1.3.0-beta02/javadoc/")
            url = new URL("https://javadoc.jitpack.io/com/github/anitrend/support-arch/support-domain/1.3.0-beta02/javadoc/")
            url = new URL("https://developer.android.com/reference/kotlin/")
            packageListUrl = new URL("https://developer.android.com/reference/androidx/package-list")
        }
    }
}

task dokkaJar(type: Jar) {
    setArchiveClassifier('javadoc')
    from dokka
}

task sourcesJar(type: Jar) {
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    setArchiveClassifier('javadoc')
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives dokkaJar
    archives classesJar
    //archives javadocJar
    archives sourcesJar
}